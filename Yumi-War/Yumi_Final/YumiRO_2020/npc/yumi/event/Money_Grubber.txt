//
// [========================================================]
// [===========           Money Grubber          ===========]
// [========================================================]
// [ Version 15                                             ]
// [========================================================]
// [ Original script by: Ivion                              ]
// [========================================================]
// [ Description:                                           ]
// [--------------------------------------------------------]
// [                                                        ]
// [ - Storage.                                             ]
// [ - Guild Storage.                                       ]
// [ - Broadcast trade.                                     ]
// [ - Cart Renting.                                        ]
// [ - Poring Shop.                                         ]
// [========================================================]
// [ Changelog: (5 last updates)                            ]
// [--------------------------------------------------------]
// [                                                        ]
// [ v11: [Ivion]                                           ]
// [ - Uso de strnpcinfo(1).                                ]
// [ - Retirado el código de conversión de variables.       ]
// [                                                        ]
// [ v12: [Ivion]                                           ]
// [ - Recolocados los npcs.                                ]
// [ - Bugfix: Espacios en el menú dinámico.                ]
// [ - Bugfix: Los intereses no se aplicaban.               ]
// [ - Bugfix: Las cantidades muy grandes producían extraños]
// [   errores.                                             ]
// [ - Configuración para intereses /1000.                  ]
// [ - El precio del almacén de la guild no se aplicará si  ]
// [   otro miembro lo está usando.                         ]
// [                                                        ]
// [ v13: [Ivion]                                           ]
// [ - Translated into english.                             ]
// [ - I've decided bank feature is useless and will no     ]
// [   longer be featured in this script.                   ]
// [ - Added Poring Shop.                                   ]
// [                                                        ]
// [ v14: [Ivion]                                           ]
// [ - Adjusted the Poring Shop prices.                     ]
// [                                                        ]
// [ v15: [Ivion]                                           ]
// [ - Increased the Bomb Poring Box price.                 ]
// [ - Added Emperium and Jewelry Box to the Poring Shop.   ]
// [ - Added new Broadcast Trade function.                  ]
// [========================================================]
// [ http://www.eathena.ws/board/index.php?showtopic=223300 ]
// [========================================================]
-	script	MEfunc	-1,{

OnInit:

// [========================================================]
// [ Configuration:                                         ]
// [--------------------------------------------------------]

	set .@n$,"Poring Coins";	// NPC name.

	// Prices: (zenys | -1 = off)
	set $@MEz[0],200;	// Storage.
	set $@MEz[1],200;	// Guild Storage.
	set $@MEz[2],10000;	// Broadcast trade.
	set $@MEz[3],5000;	// Cart renting.

	// Poring Shop:
	// getarg(0): Item id.
	// getarg(1): Price. (Poring Coins)
	// getarg(2): Color. (optional)
	callsub OnPShop,12110,5,"3355cc";	// First Aid Kit.
	callsub OnPShop,12111,5,"3355cc";	// Bundle of Food.
	callsub OnPShop,12027,5,"3355cc";	// Giggling Box.
	callsub OnPShop,12034,5,"3355cc";	// Box of Panting.
	callsub OnPShop,12257,5,"3355cc";	// Cold Medicine.
	callsub OnPShop,12109,3,"8822aa";	// Poring Box.
	callsub OnPShop,12258,26,"8822aa";	// Bomb Poring Box.
	callsub OnPShop,12999,3,"8822aa";	// Dead Branch Box.
	callsub OnPShop,14233,24,"8822aa";	// Dead Branch Box(10).
	callsub OnPShop,14234,56,"8822aa";	// Dead Branch Box(25).
	callsub OnPShop,12103,90,"8822aa";	// Bloody Branch.
	callsub OnPShop,12212,3,"996600";	// Giant Fly Wing.
	callsub OnPShop,12221,4,"996600";	// Megaphone.
	callsub OnPShop,12028,6,"996600";	// Box of Thunder.
	callsub OnPShop,12214,15,"996600";	// Convex Mirror.
	callsub OnPShop,12208,46,"996600";	// Battle Manual.
	callsub OnPShop,12210,80,"996600";	// Bubble Gum.
	callsub OnPShop,668,3,"bb22aa";		// Red Envelope.
	callsub OnPShop,664,3,"bb22aa";		// Gift Box.
	callsub OnPShop,12108,10,"bb22aa";	// Scroll Package.
	callsub OnPShop,12107,19,"bb22aa";	// Wrapped Masc.
	callsub OnPShop,12106,140,"bb22aa";	// Jewelry Box.
	callsub OnPShop,616,90,"bb22aa";	// Old Card Album.
	callsub OnPShop,714,5,"229966";	// Emperium.

// [--------------------------------------------------------]
// [ End of configuration.                                  ]
// [========================================================]

	// NPC names:
	set .@a,1;
	while(!setnpcdisplay("ME#"+.@a,.@n$+"#"+.@a)) set .@a,.@a + 1;
	set $@ME$,"^008888"+.@n$+"^000000";
	end;

OnPShop:
	set .@a,getarraysize($@MEpsi);
	if(.@a < 50) {
		set $@MEpsi[.@a],getarg(0);
		set $@MEpsp[.@a],getarg(1);
		set $@MEpsc$[.@a],getarg(2,"000000");
	}
	return;
}

// Npcs:
-	script	ME#0::ME	-1,{

	mes $@ME$;
	mes "Hello.";
	mes "How can I help you?";
	next;
	setarray .@a$,"Open Storage","Open Guild Storage","Broadcast trade","Rent a Pushcart","Poring Shop";
	while(.@a < 5) {
		if(	((.@a == 1) && !getcharid(2)) ||
			((.@a == 3) && !(getskilllv(39) && checkcart())) ||
			($@MEz[.@a] < 0)
		)
			deletearray .@a$[.@b],1;
		else {
			set .@c[.@b],.@a;
			if($@MEz[.@a]) {
				if(Zeny < $@MEz[.@a]) {
					set .@b$,"bbbbbb";
					set .@c$,"bb0000";
				}
				else {
					set .@b$,"000000";
					set .@c$,"00bb00";
				}
				set .@a$[.@b],"^"+.@b$+""+.@a$[.@b]+" ^"+.@c$+"("+$@MEz[.@a]+" z)^000000";
			}
			set .@b,.@b + 1;
		}
		set .@a,.@a + 1;
	}
	set .@a,select("^aa0000Exit^000000",.@a$[0],.@a$[1],.@a$[2],.@a$[3],.@a$[4]) - 2;
	mes $@ME$;
	if(.@a != -1) {
		set .@a,.@c[.@a];
		if(Zeny < $@MEz[.@a]) {
			mes "Sorry.";
			mes "You need to pay ^bb0000"+$@MEz[.@a]+"^000000 zenys to "+$@MEn$[.@a];
			emotion 40;
			close;
		}
		switch(.@a) {
			case 0: // Open Storage:
			case 1: // Open Guild Storage:
				mes "Ok.";
				mes "Up next I'll open your Storage...";
				close2;
				if(!.@a) openstorage;
				else {
					set .@a,guildopenstorage();
					if(.@a == 1) {
						mes $@ME$;
						mes "I'm sorry but another guild member is using the guild storage right now.";
						mes "Please wait until that person is finished.";
						emotion 17;
						close;
					}
				}
				set Zeny,Zeny - $@MEz[.@a];
				end;
				break;
			case 2: // Broadcast trade:
				mes "Which kind of trade do you want me to broadcast?";
				next;
				set .@b,select("^aa0000Exit^000000","Buy","Sell") - 1;
				mes $@ME$;
				if(.@b) {
					mes "Tell me the id of the item you want to trade.";
					next;
					input .@c;
					mes $@ME$;
					set .@d,getiteminfo(.@c,10);
					if(.@d == -1) {
						mes "Sorry.";
						mes "I haven't found that item in the item database.";
						emotion 17;
						close;
					}
					if(.@b == 1) set .@a$,"Buy";
					else set .@a$,"Sell";
					set .@b$,getitemname(.@c);
					if(.@d) set .@b$,.@b$+"["+.@d+"]";
					mes "You want to "+.@a$+": ^bb0000"+.@b$+"^000000.";
					mes "Is it right?";
					mes "Do you want me to broadcast this trade?";
					next;
					set .@b,select("^aa0000No^000000","Yes") - 1;
					mes $@ME$;
					if(.@b) {
						mes "Ok.";
						mes "Up next I'll broadcast your trade...";
						close2;
						announce "["+strcharinfo(0)+"] "+.@a$+"s: "+.@b$+".",bc_all,0x00bb00;
						specialeffect 311;
						set Zeny,Zeny - $@MEz[.@a];
						end;
					}
				}
				break;
			case 3: // Cart Renting:
				mes "Ok.";
				mes "Here you're your cart.";
				setcart 1;
				set Zeny,Zeny - $@MEz[.@a];
				mes "I hope you make a good use of it.";
				close;
				break;
			case 4: // Poring Shop:
				set .@b,countitem(7539);
				if(.@b) set .@d$,"0055bb";
				else set .@d$,"bb0000";
				mes "Currently you have ^"+.@d$+""+.@b+"^000000 Poring Coins.";
				mes "Here you can exchange your Poring Coins for some useful items.";
				next;
				set .@a,0;
				cleararray .@a$[0],"",getarraysize(.@a$);
				while(.@a < getarraysize($@MEpsi)) {
					if(.@b < $@MEpsp[.@a]) {
						set .@b$,"bbbbbb";
						set .@c$,"bb0000";
					}
					else {
						set .@b$,$@MEpsc$[.@a];
						set .@c$,"00bb00";
					}
					set .@a$[.@a],"^"+.@b$+""+getitemname($@MEpsi[.@a])+" ^"+.@c$+"("+$@MEpsp[.@a]+")^000000";
					set .@a,.@a + 1;
				}
				set .@a,select("^aa0000Exit^000000",.@a$[0],.@a$[1],.@a$[2],.@a$[3],.@a$[4],.@a$[5],.@a$[6],.@a$[7],.@a$[8],.@a$[9],.@a$[10],.@a$[11],.@a$[12],.@a$[13],.@a$[14],.@a$[15],.@a$[16],.@a$[17],.@a$[18],.@a$[19],.@a$[20],.@a$[21],.@a$[22],.@a$[23],.@a$[24],.@a$[25],.@a$[26],.@a$[27],.@a$[28],.@a$[29],.@a$[30],.@a$[31],.@a$[32],.@a$[33],.@a$[34],.@a$[35],.@a$[36],.@a$[37],.@a$[38],.@a$[39],.@a$[40],.@a$[41],.@a$[42],.@a$[43],.@a$[44],.@a$[45],.@a$[46],.@a$[47],.@a$[48],.@a$[49]) - 2;
				mes $@ME$;
				if(.@a != -1) {
					if(.@b < $@MEpsp[.@a]) {
						mes "Sorry.";
						mes "You don't have enough Poring Coins to exchange this item.";
						emotion 40;
						close;
					}
					mes "Currently you have ^"+.@d$+""+.@b+"^000000 Poring Coins.";
					mes"How many "+.@a$[.@a]+" would you like to take?";
					next;
					input .@c;
					if((.@c * $@MEpsp[.@a]) > .@b) set .@c,.@b / $@MEpsp[.@a];
					mes $@ME$;
					if(.@c) {
						mes "Ok.";
						mes "Here you are your ^"+$@MEpsc$[.@a]+""+getitemname($@MEpsi[.@a])+"^000000.";
						mes "Thank you.";
						delitem 7539,.@c * $@MEpsp[.@a];
						getitem $@MEpsi[.@a],.@c;
						emotion 15;
						close;
					}
				}
				break;
		}
	}
	mes "See you soon.";
	close2;
	emotion 12;
	end;
}

// Npcs:
alberta,195,140,4	duplicate(ME)	ME#1	542
alberta,49,250,5	duplicate(ME)	ME#2	833
aldebaran,133,118,6	duplicate(ME)	ME#3	833
aldebaran,133,238,6	duplicate(ME)	ME#4	833
alde_gld,234,152,4	duplicate(ME)	ME#5	833
amatsu,243,281,6	duplicate(ME)	ME#6	833
amatsu,101,259,4	duplicate(ME)	ME#7	833
amatsu,207,102,4	duplicate(ME)	ME#8	833
ayothaya,212,206,4	duplicate(ME)	ME#9	833
ayothaya,157,80,4	duplicate(ME)	ME#10	833
comodo,180,331,4	duplicate(ME)	ME#11	833
comodo,205,114,4	duplicate(ME)	ME#12	542
comodo,318,180,4	duplicate(ME)	ME#13	542
comodo,34,220,6	duplicate(ME)	ME#14	833
einbech,66,94,6	duplicate(ME)	ME#15	833
einbech,131,247,6	duplicate(ME)	ME#16	833
einbech,40,201,8	duplicate(ME)	ME#17	833
einbroch,76,187,2	duplicate(ME)	ME#18	833
einbroch,151,316,6	duplicate(ME)	ME#19	833
einbroch,152,51,4	duplicate(ME)	ME#20	833
einbroch,247,239,2	duplicate(ME)	ME#21	833
geffen,123,71,4	duplicate(ME)	ME#22	833
geffen,41,109,6	duplicate(ME)	ME#23	833
geffen,124,179,4	duplicate(ME)	ME#24	833
geffen,169,123,6	duplicate(ME)	ME#25	833
gef_fild13,192,246,4	duplicate(ME)	ME#26	833
gonryun,152,112,6	duplicate(ME)	ME#27	833
gonryun,170,181,2	duplicate(ME)	ME#28	833
gonryun,155,17,6	duplicate(ME)	ME#29	833
hugel,101,132,4	duplicate(ME)	ME#30	833
hugel,177,172,4	duplicate(ME)	ME#31	833
izlude,121,96,6	duplicate(ME)	ME#32	833
jawaii,135,257,4	duplicate(ME)	ME#33	833
yuno,175,91,4	duplicate(ME)	ME#34	833
yuno,66,205,4	duplicate(ME)	ME#35	897
sch_gld,291,125,4	duplicate(ME)	ME#36	897
lighthalzen,209,97,6	duplicate(ME)	ME#37	897
lighthalzen,218,324,4	duplicate(ME)	ME#38	897
lighthalzen,311,301,4	duplicate(ME)	ME#39	897
louyang,205,104,6	duplicate(ME)	ME#40	897
louyang,57,248,4	duplicate(ME)	ME#41	897
xmas,139,308,6	duplicate(ME)	ME#42	897
xmas,159,109,4	duplicate(ME)	ME#43	897
morocc,166,78,4	duplicate(ME)	ME#44	897
morocc,45,174,4	duplicate(ME)	ME#45	897
morocc,166,291,4	duplicate(ME)	ME#46	897
morocc,277,197,8	duplicate(ME)	ME#47	897
morocc,34,292,4	duplicate(ME)	ME#48	897
moscovia,238,196,4	duplicate(ME)	ME#49	897
moscovia,158,63,4	duplicate(ME)	ME#50	897
niflheim,27,157,6	duplicate(ME)	ME#51	897
niflheim,200,186,4	duplicate(ME)	ME#52	897
payon,213,74,6	duplicate(ME)	ME#53	542
payon,162,49,2	duplicate(ME)	ME#54	542
payon,232,318,4	duplicate(ME)	ME#55	542
payon,63,122,6	duplicate(ME)	ME#56	542
pay_gld,201,159,4	duplicate(ME)	ME#57	897
junon,250,165,3	duplicate(ME)	ME#58	542
prontlegend,210,205,4	duplicate(ME)	ME#59	542
//prontera,164,172,4	duplicate(ME)	ME#60	897
//prontera,261,213,4	duplicate(ME)	ME#61	897
//prontera,161,314,4	duplicate(ME)	ME#62	897
prt_gld,166,77,4	duplicate(ME)	ME#63	897
rachel,262,136,4	duplicate(ME)	ME#64	897
rachel,150,142,4	duplicate(ME)	ME#65	897
rachel,135,47,4	duplicate(ME)	ME#66	897
rachel,37,131,6	duplicate(ME)	ME#67	897
aru_gld,179,328,6	duplicate(ME)	ME#68	897
umbala,119,129,8	duplicate(ME)	ME#69	897
umbala,163,251,4	duplicate(ME)	ME#70	897
veins,207,235,4	duplicate(ME)	ME#71	897
veins,222,354,4	duplicate(ME)	ME#72	897
veins,155,62,6	duplicate(ME)	ME#73	897
mid_camp,217,287,4	duplicate(ME)	ME#74	897
mid_camp,136,234,6	duplicate(ME)	ME#75	897
mid_camp,254,231,8	duplicate(ME)	ME#76	897
manuk,283,127,2	duplicate(ME)	ME#77	897
manuk,182,179,6	duplicate(ME)	ME#78	897
alexa_in,89,158,5	duplicate(ME)	Mr Smith#79	56
