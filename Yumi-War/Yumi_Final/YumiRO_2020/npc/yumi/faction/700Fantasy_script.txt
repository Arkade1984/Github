// ==== *Athena Script =================================
// Kamishi's Palettes Protection V.1.0
// Never show this file to anyone, except yourself. ;)
// =====================================================

// =====================================================
// Initialize the hash table required to access secret 
// palette file numbers.
// =====================================================

prontera,179,27,5	script	#121345	111,{
	end;
	OnPCLoginEvent:
		if (!setPaletteToProtectedNumber == 2){
			callfunc("setlook2", 6, realPlayerLookHair);
			callfunc("setlook2", 7, realPlayerLookClothes);
			set setPaletteToProtectedNumber,2;
		}
		end;

	OnInit:
		set $paletteSECRET1, 179;
		set $paletteMAGIC2, 14;
		set $paletteSECRET3, 7000;
		set $paletteOFFSET, 600;
		setarray $paletteHash[0],136,19,63,169,37,117,12,166,18,81,121,9,105,69,161,175,120,77,43,116,4,139,130,132,115,25,171,55,155,176,47,145,24,45,167,44,88,133,87,102,148,49,75,92,41,159,78,83,177,172,13,74,68,60,20,131,40,107,67,134,127,138,23,168,143,72,15,137,64,144,174,29,17,30,122,65,80,54,100,178,38,71,124,119,57,142,34,0,36,104,76,123,73,147,94,42,109,135,59,162,11,91,152,146,140,5,108,48,160,157,125,129,99,31,6,53,58,35,39,106,61,151,89,150,118,21,26;
		end;
}


// ====== Access the secret file palette number. =======
// Method replacement for setlook, changelook, getlook.
// ==> setLook2, getLook2, changeLook2
// =====================================================


function	script	getProtectedPal	{
	set @i, getarg(0);
	
	if(@i == 0) return 0;

	set @type, getarg(1);
	if(@type == 7){
		if(@i < 5) return @i; // set 5 to your unprotected clothes palette #.
	}
	if(@type == 6){
		if(@i < 10) return @i; // set 10 to your unprotected hair palette #.
	}

	set @m, @i / 127; 
	set @d, $paletteHash[(@i +($paletteMAGIC2*@m)) % 127] + ($paletteSECRET1* @m);
	set @d, $paletteSECRET3-@d;
	return @d+$paletteOFFSET;
}

function	script	setlook2	{
	//Save current value:
	set @type, getarg(0);
	set @i, getarg(1);
	
	
	if(@type == 6){
		set realPlayerLookHair, @i;		
	
	}else if (@type == 7){
		set realPlayerLookClothes, @i;		
	}else{
		setlook @type, @i;
		return;
	}
	
	setlook @type, callfunc("getProtectedPal", @i, @type);
	
	return;
}

function	script	changelook2	{
	//Save current value:
	set @type, getarg(0);
	set @i, getarg(1);
	
	
	if((@type == 6) || (@type == 7)){
		changelook @type, callfunc("getProtectedPal", @i, @type);
	}else{
		changelook @type, @i;
	}
	
	
	return;
}

function	script	getlook2	{
	set @type, getarg(0);
	
	if(@type == 6){
		return realPlayerLookHair;		
	}else if (@type == 7){
		return realPlayerLookClothes;		
	}else{
		return getlook(@type);
	}
}

		