prontlegend,182,221,5	script	Identifier	407,{
	getinventorylist;
	for( set .@i,0; .@i < @inventorylist_count; set .@i, .@i + 1 ) {
		if ( @inventorylist_identify[.@i] == 1 ) continue;

		// Merchant's Identify skill
		if ( getskilllv(40) == 1 && Sp >= 10 ) heal 0,-10;

		// check if there is any Magnifier
		else if ( countitem(611) ) delitem 611,1;

		// base on Magnifier price, Rogue's Compulsion Discount skill
		else if ( getskilllv(224) && Zeny >= ( 100 - ( 5 + 4 * getskilllv(224) ) )* 2/5 )
			set Zeny, Zeny - ( 100 - ( 5 + 4 * getskilllv(224) ) )* 2/5;

		// base on Magnifier price, Merchants that not enough Sp and learned Discount skill
		else if ( getskilllv(37) && Zeny >= ( 100 - ( 5 + 2 * getskilllv(37) ) )* 2/5 )
			set Zeny, Zeny - ( 100 - ( 5 + 2 * getskilllv(37) ) )* 2/5;

		// if none of the above, deduct the Zeny
		else if ( Zeny >= 40 )  set Zeny, Zeny - 40;

		// or stop the script if there the person not enough money
		else break;
		delitem2 @inventorylist_id[.@i],1,0,0,0,0,0,0,0;
		getitem @inventorylist_id[.@i],1;
	}
}



morocc,162,97,3	duplicate(Identifier)	Identifier#h1-2	407
ayothaya,155,116,3	duplicate(Identifier)	Identifier#h1-3	407
gonryun,164,122,3	duplicate(Identifier)	Identifier#h1-4	407
izlude,132,118,3	duplicate(Identifier)	Identifier#h1-5	407
moscovia,229,195,3	duplicate(Identifier)	Identifier#h1-6	407
lighthalzen,151,101,3	duplicate(Identifier)	Identifier#h1-7	407
hugel,91,145,3	duplicate(Identifier)	Identifier#h1-8	407
geffen,115,73,3	duplicate(Identifier)	Identifier#h1-9	407
rachel,137,105,3	duplicate(Identifier)	Identifier#h1-10	407
splendide,192,152,3	duplicate(Identifier)	Identifier#h1-11	407
brasilis,189,222,3	duplicate(Identifier)	Identifier#h1-12	407
prt_in,276,173,5	duplicate(Identifier)	Identifier#h1-13	407
