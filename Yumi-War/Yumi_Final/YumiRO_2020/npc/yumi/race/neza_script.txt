// ==== *Athena Script =================================
// Kamishi's Palettes Protection V.1.0
// Never show this file to anyone, except yourself. ;)
// =====================================================

// =====================================================
// Initialize the hash table required to access secret 
// palette file numbers.
// =====================================================

prontera,179,27,5	script	#121345	111,{
	end;
	/*OnPcLoginEvent:
		if (!setPaletteToProtectedNumber){
			callfunc("setlook2", 6, getlook(6));
			callfunc("setlook2", 7, getlook(7));
			set setPaletteToProtectedNumber,1;
		}
		end;*/

	OnInit:
		set $paletteSECRET1, 333;
		set $paletteMAGIC2, 30;
		set $paletteSECRET3, 5000;
		set $paletteOFFSET, 700;
		setarray $paletteHash[0],26,170,53,187,322,327,4,203,266,318,306,284,22,9,325,5,192,287,116,326,130,159,43,262,321,128,32,277,220,198,123,103,84,271,40,186,242,87,257,252,213,27,45,11,250,272,158,0,323,237,216,300,97,108,76,64,49,107,129,28,67,253,42,324,52,126,86,209,41,62,127,268,195,320,153,183,298,61,211,24,310,160,36,202,33,273,121,13,260,18,16,208,235,151,161,201,182,73,180,14,106,136,238,21,259,178,94,254,104,109,78,68,168,83,44,133,227,289,172,164,229,29,309,244,311,143,245;
		end;
}


// ====== Access the secret file palette number. =======
// Method replacement for setlook, changelook, getlook.
// ==> setLook2, getLook2, changeLook2
// =====================================================


function	script	getProtectedPal	{
	set @i, getarg(0);
	
	if(@i == 0) return 0;

	set @type, getarg(1);
	if(@type == 7){
		if(@i < 0) return @i; // set 0 to your unprotected clothes palette #.
	}
	if(@type == 6){
		if(@i < 0) return @i; // set 0 to your unprotected hair palette #.
	}

	set @m, @i / 127; 
	set @d, $paletteHash[(@i +($paletteMAGIC2*@m)) % 127] + ($paletteSECRET1* @m);
	if (Sex == 0) { set @d, $paletteSECRET3-@d; }
	return @d+$paletteOFFSET;
}

function	script	setlook2	{
	//Save current value:
	set @type, getarg(0);
	set @i, getarg(1);
	
	
	if(@type == 6){
		set realPlayerLookHair, @i;		
	
	}else if (@type == 7){
		set realPlayerLookClothes, @i;		
	}else{
		setlook @type, @i;
		return;
	}
	
	setlook @type, callfunc("getProtectedPal", @i, @type);
	
	return;
}

function	script	changelook2	{
	//Save current value:
	set @type, getarg(0);
	set @i, getarg(1);
	
	
	if((@type == 6) || (@type == 7)){
		changelook @type, callfunc("getProtectedPal", @i, @type);
	}else{
		changelook @type, @i;
	}
	
	
	return;
}

function	script	getlook2	{
	set @type, getarg(0);
	
	if(@type == 6){
		return realPlayerLookHair;		
	}else if (@type == 7){
		return realPlayerLookClothes;		
	}else{
		return getlook(@type);
	}
}

		