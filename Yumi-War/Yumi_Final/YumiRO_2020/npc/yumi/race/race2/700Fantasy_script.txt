// ==== *Athena Script =================================
// Kamishi's Palettes Protection V.1.0
// Never show this file to anyone, except yourself. ;)
// =====================================================

// =====================================================
// Initialize the hash table required to access secret 
// palette file numbers.
// =====================================================

prontera,179,27,5	script	#121345	111,{
	end;
	/*OnPcLoginEvent:
		if (!setPaletteToProtectedNumber){
			callfunc("setlook2", 6, getlook(6));
			callfunc("setlook2", 7, getlook(7));
			set setPaletteToProtectedNumber,1;
		}
		end;*/

	OnInit:
		set $paletteSECRET1, 454;
		set $paletteMAGIC2, 37;
		set $paletteSECRET3, 5000;
		set $paletteOFFSET, 1;
		setarray $paletteHash[0],441,108,349,26,404,436,20,40,337,435,4,126,125,90,386,416,179,374,79,318,14,378,153,23,22,137,111,210,307,376,136,451,415,24,276,226,244,434,308,11,231,437,224,15,440,265,411,49,409,123,288,347,236,391,115,319,205,335,183,380,257,390,145,130,429,127,375,303,192,247,333,281,206,297,80,260,393,35,118,443,138,216,6,398,175,196,258,70,228,10,298,355,160,104,214,249,97,38,230,220,344,368,332,267,156,348,285,438,34,41,354,169,227,120,186,361,64,180,389,139,385,334,42,91,317,262,245;
		end;
}


// ====== Access the secret file palette number. =======
// Method replacement for setlook, changelook, getlook.
// ==> setLook2, getLook2, changeLook2
// =====================================================


function	script	getProtectedPal	{
	set @i, getarg(0);
	
	if(@i == 0) return 0;

	set @type, getarg(1);
	if(@type == 7){
		if(@i < 0) return @i; // set 0 to your unprotected clothes palette #.
	}
	if(@type == 6){
		if(@i < 0) return @i; // set 0 to your unprotected hair palette #.
	}

	set @m, @i / 127; 
	set @d, $paletteHash[(@i +($paletteMAGIC2*@m)) % 127] + ($paletteSECRET1* @m);
	if (Sex == 0) { set @d, $paletteSECRET3-@d; }
	return @d+$paletteOFFSET;
}

function	script	setlook2	{
	//Save current value:
	set @type, getarg(0);
	set @i, getarg(1);
	
	
	if(@type == 6){
		set realPlayerLookHair, @i;		
	
	}else if (@type == 7){
		set realPlayerLookClothes, @i;		
	}else{
		setlook @type, @i;
		return;
	}
	
	setlook @type, callfunc("getProtectedPal", @i, @type);
	
	return;
}

function	script	changelook2	{
	//Save current value:
	set @type, getarg(0);
	set @i, getarg(1);
	
	
	if((@type == 6) || (@type == 7)){
		changelook @type, callfunc("getProtectedPal", @i, @type);
	}else{
		changelook @type, @i;
	}
	
	
	return;
}

function	script	getlook2	{
	set @type, getarg(0);
	
	if(@type == 6){
		return realPlayerLookHair;		
	}else if (@type == 7){
		return realPlayerLookClothes;		
	}else{
		return getlook(@type);
	}
}

		