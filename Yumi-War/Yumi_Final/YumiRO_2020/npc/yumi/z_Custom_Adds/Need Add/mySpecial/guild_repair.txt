//===== eAthena Script =======================================
//= Guildhouse Repairmann
//===== By: ==================================================
//= Masao
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= eAthena SVN
//===== Description: =========================================
//= Repairs the Equipment of a Player for a small fee of
//= 5k Zeny.
//===== Additional Comments: =================================
//= 1.0 First version
//============================================================ 

rent_in,5,16,4	script	Guild Repairmen::GRepair	86,{
	
	
	set .@n$,"[Guild Repairmen]";
	set .@repairprice,5000; //Repairment Fee.

	mes .@n$;
	mes "Hi "+strcharinfo(0)+",";
	mes "i can repair your equipment which got destroyed during battle.";
	next;
	switch(select("Yes, do it!:No, thanks.")) {
	case 1:
		set .@checkitem,1;
		while (1) {
			if (getbrokenid(.@checkitem) == 0) {
				break;
			}
			set .@checkitem,.@checkitem+1;
		}
		set .@checkitem,.@checkitem-1;
		if (!.@checkitem) {
			mes .@n$;
			mes "Wow not bad, your Equipment is in great condition!";
			mes "If everyone would handle his Equipment as good as you i would soon have no Job! *haha*";
			close;
		}
		mes .@n$;
		mes "Hmm...";
		mes "Let's see...";
		mes "That many Equipment pieces are destroyed:";
		mes "" + .@checkitem + "";
		mes "Would you like me to repair them all now?";
		next;
		set .@totalcost,.@repairprice*.@checkitem;
		mes .@n$;
		mes "Each repair costs you " + .@repairprice + " Zeny. So to repair it all it would cost you a total of " + .@totalcost + " Zeny!";
		mes "Should i repair your equipment now?";
		next;
		switch(select("Yes, please.:No, thanks.")) {
		case 1:
			if (Zeny < .@totalcost) {
				mes .@n$;
				mes "Whoa whoa...";
				mes "Sorry, but you're not having enough money with you.";
				close;
			}
			set .@checkitem2,1;
			while (1) {
				if (getbrokenid(.@checkitem2) == 0) {
					break;
				}
				set .@checkitem2,.@checkitem2+1;
			}
			set .@checkitem2,.@checkitem2-1;
			if (.@checkitem == .@checkitem2) {
				set zeny,zeny-.@totalcost;
				while (.@checkitem) {
					repair(.@checkitem);
					set .@checkitem,.@checkitem-1;
				}
				mes .@n$;
				mes "Allright, everything got repaired and is as good as new, take care of you and your equipment.";
				close;
			}
			else {
				mes .@n$;
				close;
			}
		case 2:
			mes .@n$;
			close;
		}
	case 2:
		mes .@n$;
		mes "Ok, bye.";
		close;
	}
}
	
rent_in2,5,16,4	duplicate(GRepair)	Guild Repairmen#02	86
rent_in3,5,16,4	duplicate(GRepair)	Guild Repairmen#03	86
rent_in4,5,16,4	duplicate(GRepair)	Guild Repairmen#04	86
rent_in5,5,16,4	duplicate(GRepair)	Guild Repairmen#05	86
rent_in6,5,16,4	duplicate(GRepair)	Guild Repairmen#06	86