
//MAGNIFIER

prontlegend,197,197,4	script	Identifier	900,{

// Language System
// ===============
// You'll need to set this variable for Players by another NPC
// or set it here (set @language to 0 or 1 if you want the NPC
// to be in one language all the time, if you want it to be in
// english you don't have to change it, english is default).
// 0 = english (default) | 1 = german

set @language, #language;
if (@language > 1) set @language, 0;

// Price
// =====
// This is the Price in Zeny for one identified Item

set @price, 100;
// Actual Script Begins Here
// =========================

mes "[Deckard Cain]";
if (@language == 0) mes "Hello there!";
if (@language == 0) mes "I can identify all your unidentified items.";
if (@language == 0) mes "It's just "+@price+" Zeny each item.";
if (@language == 1) mes "Hallo Reisender!";
if (@language == 1) mes "Ich kann all Deine unidentifizierten Items f?r Dich identifizieren.";
if (@language == 1) mes "Es kostet nur "+@price+" Zeny je Item.";
next;
if (@language == 0) menu "Sure.",-,
"No thanks...",L_Bye;
if (@language == 1) menu "Gerne.",-,
"Nein Danke...",L_Bye;

getinventorylist;
set @identify, 0;

for(set @i, 0; @i < @inventorylist_count; set @i, @i + 1) {
if (@inventorylist_identify[@i] == 0) set @identify, @identify + 1;
}

if (@identify == 0) goto L_Nothing;

mes "[Deckard Cain]";
if (@identify < 2) {
if (@language == 0) mes "You've got one unidentified item in your inventory.";
if (@language == 0) mes "I'll identify it for "+@price+" Zeny.";
if (@language == 1) mes "Du hast ein unidentifiziertes Items in Deinem Inventar.";
if (@language == 1) mes "F?r "+@price+" Zeny kann ich es identifizieren.";
} else {
if (@language == 0) mes "You've got "+@identify+" unidentified items in your inventory.";
if (@language == 0) mes "I'll identify them for "+(@identify * @price)+" Zeny.";
if (@language == 1) mes "Du hast "+@identify+" unidentifizierte Items in Deinem Inventar.";
if (@language == 1) mes "F?r "+(@identify * @price)+" Zeny kann ich sie alle identifizieren.";
}
if (@language == 0) mes "If you've got any ^00AA00Magnifiers^000000, I'll take those instead.";
if (@language == 0) mes "Shall I start now?";
if (@language == 1) mes "Wenn Du einige ^00AA00Magnifier^000000 hast, werde ich die benutzen.";
if (@language == 1) mes "Soll ich jetzt anfangen?";
next;
if (@language == 0) menu "Ok, let's go!",-,
"What a ripoff...",L_Bye;
if (@language == 1) menu "Ok, leg los!",-,
"Was f?r ein Wucher...",L_Bye;

getinventorylist;
set @identified, 0;

for(set @i, 0; @i < @inventorylist_count; set @i, @i + 1) {
if (@inventorylist_identify[@i] == 0) {
if (countitem(611) > 0) delitem 611,1;
else if (Zeny < @price) goto L_NoZeny;
else set Zeny, Zeny - @price;
delitem2 @inventorylist_id[@i],1,0,0,0,0,0,0,0;
getitem @inventorylist_id[@i],1;
set @identified, @identified + 1;
}
}

mes "[Deckard Cain]";
if (@language == 0) mes "Well, I identified everything!";
if (@language == 0) mes "I've identified a total of "+@identified+" items for you.";
if (@language == 1) mes "So, alles identifiziert!";
if (@language == 1) mes "Ich habe insgesamt "+@identified+" Items f?r Dich identifiziert.";
mes "Bye~";
close;

L_Nothing:
mes "[Deckard Cain]";
if (@language == 0) mes "You don't have any unidentified Items.";
if (@language == 1) mes "Du hast keinerlei nicht identifizierte Items.";
close;

L_NoZeny:
mes "[Deckard Cain]";
if (@language == 0) mes "You don't have enough Zeny.";
if (@language == 1) mes "Du hast nicht gen?gend Zeny.";
close;

L_Bye:
mes "[Deckard Cain]";
mes "...";
close;

}