//Credits to Mystogan,hellflaem,~AnnieRuru~,Swapnil123,KeyWorld
//I m using item 7623 Just replace the item with the one you want 
//the players to exchange in order to able to rename their character.
//Scripts allows only a-z,A-Z,0-9 and white space in their name.
//It checks for party/Guild ...
//==============================================================
//===================== The Name Changer =========================
//==============================================================

itemmall,188,68,4	script	Name Changer	894,{
// ============ Npc's Name ====================
set .@npcname$,"[ ^002f77Mystogan's Name Changer^000000 ]";
// ============ Name Filters ======================
setarray .filter$[0],
	"Admin",
	"Game Master",
	"[GM]",
	"GM ","!";

// ============ Main Script =======================
offers:
	mes .@npcname$;
	mes "Welcome to Mystogan's Name Changer!";
	mes "";
	mes "^FF0000 - We can change your name if you have a Rename Ticket.";
	next;
	if((getcharid(2) > 0) || (getcharid(1) > 0))
	{
		mes .@npcname$;
		mes "You should leave your guild/party before changing your name.";
		close;
	}
	else if(countitem(7623) < 1)
	{
		mes .@npcname$;
		mes "You do not have a Rename Ticket.";
		close;
	}
	else
	{
		inputs:
		mes .@npcname$;
		mes "Please enter the name you desire:";
		if (input(.@newname$, 4, 23))
		{
			mes "^ff0000 - Must be atleast 4-23 characters^000000";
			next;
			goto inputs;
		}
		
		if ( callfunc( "check_string", .@newname$ ) == 0 ) {
				mes "^ff0000 - Invalid name^000000";
				mes "You can only use a-z A-Z and 0-9";
				next;
				goto inputs;
		}
		for (set .@i , 0; .@i < getarraysize ( .filter$ ); set .@i, .@i + 1)
		{
			if(compare(.@newname$, .filter$[.@i]))
			{
				mes "^ff0000 - Invalid name^000000";
				next;
				goto inputs;
			}
		}
		
		mes .@npcname$;
		mes "Do you want to continue?";
		mes "";
		mes " ^ff0000- You will be kicked to complete the name change.^000000";
		mes "";
		mes "^ff0000- Then just login again so your character name will be changed.^000000";
		if(select("No:Yes")==1) close;
		next;
		// Checks if the name is available
		query_sql "SELECT `account_id` FROM `char` WHERE `name` = '"+escape_sql(.@newname$)+"'",.@aid;
		if(.@aid)
		{
			mes "^FF0000 - Not Available^000000";
			next;
			goto inputs;
		}
		else 
		{
			mes "^006633 - Available^000000";
			mes .@npcname$;
			delitem 7623,1;
			query_sql "UPDATE `char` SET name = '"+escape_sql(.@newname$)+"' WHERE char_id = "+getcharid(0)+";";
			mes "Your name ^ff0000"+strcharinfo(0)+"^000000 has changed into ^ff0000"+.@newname$+"^000000.";
			close2;
			atcommand "@kick "+strcharinfo(0);
			end;
		} // The players name has been changed.
	}
} // End of Script.




function	script	check_string	{

	set .@str$, getarg(0);
	set .@len, getstrlen(.@str$);
	set .@key$, "|^" + rand(0xffffff) + "^|";
	set .@tmp$, "";

	setarray .@char_list$
		,"A","B","C","D","E","F","G","H","I","J","K","L","M", "N","O","P","Q","R","S","T","U","V","W","X","Y","Z"
		// compare isn't case sensitive, so don't need to add "a", "b", "c", "d", "e", ...
		,"1","2","3","4","5","6","7","8","9","0"," "
	;
	set .@size, getarraysize(.@char_list$);

	while ( .@i<.@len ) {
		set .@j, 0;

		while ( .@j < .@size ) {
			if ( compare( .@key$ + .@str$, .@key$ + .@tmp$ + .@char_list$[.@j] ) ) {
				set .@tmp$,.@tmp$+.@char_list$[.@j];
				break;
			}
			set .@j, .@j+1;
		}

		if ( .@j == .@size )
			return 0;

		set .@i, .@i+1;
		sleep2( .@i % 20 == 0 );
	}

	return 1;
}